apply plugin: 'com.android.application'
apply plugin: 'deploygate'

def releaseSigningProperties = "${System.env["HOME"]}/.gradle/signing/release.properties"

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    defaultConfig {
        applicationId "org.hogel.naroubrowser"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 1_000_000
        versionName "1.0.0"
    }

    signingConfigs {
        release {
            if (new File(releaseSigningProperties).exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(file(releaseSigningProperties)))
                storeFile file(props['STORE_FILE'])
                storePassword props['STORE_PASSWORD']
                keyAlias props['KEY_ALIAS']
                keyPassword props['KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        debug {
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
}

deploygate {
    userName = "${project.property("deploygate_user")}"
    token = "${project.property("deploygate_token")}"

    apks {
        release {
            sourceFile = file("build/outputs/apk/app-release.apk")
        }

        debug {
            sourceFile = file("build/outputs/apk/app-debug.apk")
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:support-annotations:22.0.0'
    compile 'com.google.android.gms:play-services-base:6.5.87'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'org.sqldroid:sqldroid:1.0.3'
    compile 'org.flywaydb:flyway-core:3.2.1'

    compile 'com.google.guava:guava:18.0'
    compile 'org.roboguice:roboguice:3.0.1'

    provided "org.projectlombok:lombok:1.12.6"
    provided 'org.roboguice:roboblender:3.0.1'

    debugCompile 'com.facebook.stetho:stetho:1.0.1'
}
