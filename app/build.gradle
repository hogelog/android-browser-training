apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'
apply plugin: 'com.github.gfx.ribbonizer'

def releaseSigningProperties = "${System.env["HOME"]}/.gradle/signing/narou_browser.properties"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "org.hogel.naroubrowser"
        minSdkVersion 23
        targetSdkVersion 23
        versionCode 1_000_000
        versionName "1.0.0"
        resConfigs "ja"
    }

    signingConfigs {
        release {
            if (new File(releaseSigningProperties).exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(file(releaseSigningProperties)))
                storeFile file(props['STORE_FILE'])
                storePassword props['STORE_PASSWORD']
                keyAlias props['KEY_ALIAS']
                keyPassword props['KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'
    compile 'com.android.support:customtabs:23.1.1'

    compile 'com.google.android.gms:play-services-base:8.4.0'
    compile 'com.google.android.gms:play-services-analytics:8.4.0'

    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'

    compile 'com.google.guava:guava:18.0'

    compile 'org.roboguice:roboguice:3.0.1'
    provided 'org.roboguice:roboblender:3.0.1'
    apt 'org.roboguice:roboblender:3.0.1'

    provided "org.projectlombok:lombok:1.16.6"
	apt "org.projectlombok:lombok:1.16.6"

    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true
    }

    debugCompile 'com.facebook.stetho:stetho:1.2.0'
}
